##########
# Analysis options
##########

# Specify tree file
TREE_FILE = args[1]

# Prior on BDPSTP
BDP_PRIOR = args[2]

# Hyperpriors (for time-varying models)
BDP_HYPERPRIOR = args[3]

# Should we estimate mass extinctions?
NUM_EXPECTED_ME = args[4]

# The probability of extinction at fossilization (treatment)
treatement_probability <- args[5]

# The probability of extinction at fossilization (treatment)
USE_TREE_SAMPLE <- (args[6] == "sample")

# Number of MCMC replicates
NUM_REPS = args[7]

# For reproducibility
SEED = args[8]

# The output directory
OUTPUT_DIR = args[9]

seed(SEED)

# Specify taxon file
TAXON_FILE = "crocs_taxa.txt"

# What should we condition the tree on?
CONDITION = "time"

# set number of speciation_rate regimes (only used for MRF models)
NUM_INTERVALS = 100

# number of breaks between regimes, for convenience (only used for MRF models)
NUM_BREAKS = NUM_INTERVALS - 1

# number of MCMC iterations
NUM_MCMC_ITERATIONS = 5000

# the thinning between MCMC samples
SAMPLING_FREQ = 1


##########
# Get data
##########

observed_trees <- readTrees("data/" + TREE_FILE)
observed_tree  <- observed_trees[1]


if ( USE_TREE_SAMPLE ) {
  for (i in 1:20) {
    tmp_trees[i] <- observed_trees[i]
  }
  observed_trees <- tmp_trees
}

taxa <- readTaxonData("data/" + TAXON_FILE,delim=TAB)

# Get some useful variables from the data. We need these later on.
num_fossils <- observed_tree.getFossils().size()
num_species <- observed_tree.ntips()
num_extant_taxa <- num_species - num_fossils
root_height <- observed_tree.rootAge()


moves = VectorMoves()
monitors = VectorMonitors()


##########
# Constant parameters
##########

sampling_at_present <- num_extant_taxa / 25

expected_number_of_mass_extinctions <- NUM_EXPECTED_ME

##########
# Mass extinction model
##########
# ME model will fill Mu with 0 if we do not want to infer mass extinctions
# This only works for time-varying tree priors, otherwise we simply never declare Mu
if (BDP_PRIOR != "CRBDP") {
  source("src/ME.Rev")
}

ME_PREFIX = "_ME_prior_" + NUM_EXPECTED_ME

##########
# Tree model
##########

source("src/" + BDP_PRIOR + ".Rev")

if ( USE_TREE_SAMPLE ) {

  ### clamp the model with the "observed" tree
  timetrees.clamp( observed_trees )

} else {

  ### clamp the model with the "observed" tree
  timetree.clamp( observed_tree )

}


##########
# The model DAG
##########

### workspace model wrapper ###
mymodel = model(sampling_at_present)

##########
# The monitors
##########

### set up the monitors that will output parameter values to file and screen
monitors[1] = mnModel(filename=OUTPUT_DIR + "/" + BDP_PRIOR + ME_PREFIX + "_" + TREE_FILE + ".log",printgen=SAMPLING_FREQ, separator = TAB)
monitors[2] = mnScreen(printgen=100)

##########
# Run MCMC
##########

### workspace mcmc ###
mymcmc = mcmc(mymodel, monitors, moves, nruns=NUM_REPS)

### Use burnin to tune the MH proposals ###
mymcmc.burnin(generations=NUM_MCMC_ITERATIONS / 10.0,tuningInterval=100)

### run the MCMC ###
mymcmc.run(generations=NUM_MCMC_ITERATIONS)


q()
